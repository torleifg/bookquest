plugins {
    id 'buildlogic.java-library-conventions'
    alias libs.plugins.openapi.generator
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation libs.commons.lang

    implementation project(':core')

    testImplementation libs.wiremock.spring.boot
}

openApiGenerate {
    generatorName = 'java'
    inputSpec = layout.projectDirectory.file('src/main/resources/bibbi/bibbi.json').asFile.absolutePath
    outputDir = layout.buildDirectory.dir('generated/sources/openapi').get().asFile.absolutePath
    modelPackage = 'no.bs.bibliografisk.model'
    generateModelTests = false
    configOptions = [
            'library'               : 'restclient',
            'useJakartaEe'          : 'true',
            'openApiNullable'       : 'false',
            'enumUnknownDefaultCase': 'true'
    ]
    globalProperties = [
            apis  : 'false',
            models: ''
    ]
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/sources/openapi/src/main/java"
        }
    }
}
