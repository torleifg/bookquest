plugins {
    id 'io.spring.dependency-management'
}

ext {
    springBootVersion = '3.4.3'
    springAiVersion = '1.0.0-M6'
    marc4jVersion = '2.9.6'
    swaggerParserVersion = '2.1.22'
    jaxbVersion = '4.0.2'
    wiremockVersion = '3.7.0'
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }
}

subprojects {
    group = 'com.github.torleifg'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
        }
        dependencies {
            dependency "org.marc4j:marc4j:${marc4jVersion}"
            dependency "io.swagger.parser.v3:swagger-parser:${swaggerParserVersion}"
            dependency "jakarta.xml.bind:jakarta.xml.bind-api:${jaxbVersion}"
            dependency "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
            dependency "org.wiremock.integrations:wiremock-spring-boot:${wiremockVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add('-parameters')
    }
}